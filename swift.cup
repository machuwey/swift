import java_cup.runtime.*;
import java.util.*;
action code {:
        Stack<String> identificadores = new Stack<String>();

        public void inicializarVariables(Object valor, Integer tipo, int profundidad, Integer esGlobal, boolean asignado) {
                for (String identificador : identificadores) {
                        TablaSimbolos.add(identificador, valor, tipo, profundidad, esGlobal, asignado);
                }
                identificadores.clear();
        }
       
:}
/* TERMINALES */
terminal    IF, ELSE, DO, WHILE, FOR, PRINT;        // Funciones del lenguaje
terminal    EQ, NE, LT, LE, GT, GE, NO, AND, OR;    // Operadores lógicos
terminal    SUM, RES, MUL, DIV, NEG, IGUAL;         // Operadores matemáticos
terminal    AP, CP, AC, CC, AL, CL;                 // Aperturas y cierres
terminal    PYC, PP, COMA;                                  // Fin de línea
terminal    EOL;                           // Comentarios
terminal    String  IDENT;
terminal    Integer     NUM;
terminal    Double     DOUBLE;
terminal    VAR,LET;
terminal    TRUE,FALSE;
terminal    DOUBLE_TYPE, INT_TYPE, BOOL_TYPE;


non terminal lineas;
non terminal linea;
non terminal expresion;
non terminal sentencia;
non terminal endline;
non terminal tipo;
non terminal listaIdent;
non terminal varLet;
non terminal ArrayList<Object> array;
non terminal objetos_finales;

precedence left SUM, RES;
precedence left MUL, DIV;
precedence left EQ, LE, GE;
precedence left NE, LT, GT;
precedence left NO, AND, OR;

precedence  nonassoc    AP, CP;
precedence  nonassoc    AC, CC;
precedence  nonassoc    AL, CL;

start with lineas;

lineas ::= linea endline lineas
         |linea endline
         |linea;

endline ::= endline EOL
          | endline PYC EOL {:System.out.println("error"); System.exit(0);:}
          | EOL
          ;

linea ::= sentencia PYC
        | sentencia
        ;

sentencia ::= sentencia PYC expresion
        | expresion
        ;

expresion ::= PRINT AP expresion:e CP {:System.out.println(e);:}
            | expresion:e SUM expresion:e2 {:RESULT = operations.sumar(e, e2);:}
            | expresion:e RES expresion:e2 {:RESULT = operations.restar(e, e2);:}
            | expresion:e MUL expresion:e2 {:RESULT = operations.multiplicar(e, e2);:}
            | expresion:e DIV expresion:e2 {:RESULT = operations.dividir(e, e2);:}
            | expresion:e OR expresion:e2 {:RESULT = ((boolean)e || (boolean)e2);:}
            | expresion:e AND expresion:e2 {:RESULT = ((boolean)e && (boolean)e2);:}
            | expresion:e EQ expresion:e2 {:RESULT = operations.eq(e, e2);:}
            | expresion:e NE expresion:e2 {:RESULT = (e != e2);:}
            | expresion:e LT expresion:e2 {:RESULT = operations.lt(e, e2);:}
            | expresion:e LE expresion:e2 {:RESULT = operations.le(e, e2);:}
            | expresion:e GT expresion:e2 {:RESULT = operations.gt(e, e2);:}
            | expresion:e GE expresion:e2 {:RESULT = operations.ge(e, e2);:}
            | varLet:g listaIdent tipo:v IGUAL expresion:e2 {: inicializarVariables(e2,0,v!=null?(Integer)v:0,(Integer)g, true);:}
            | varLet:g listaIdent tipo:v {: inicializarVariables(null,0,v!=null?(Integer)v:0,(Integer)g, false);:}
            | IDENT:e IGUAL expresion:e2 {: TablaSimbolos.replace(e, e2,0,true);:}
            | AP expresion:e CP {: RESULT = e;:}
            | IDENT:e {:RESULT=TablaSimbolos.get(e);:}
            | NO IDENT:e {:RESULT=!(boolean)TablaSimbolos.get(e);:}   
            | AC array:a CC {:RESULT=a;:}
            | IDENT:e AC NUM:n CC {:ArrayList<Object> a = (ArrayList)TablaSimbolos.get(e); RESULT=a.get(n);:}
            | IDENT:e AC NUM:n CC IGUAL expresion:o {:TablaSimbolos.modifyArray(e, n, o);:}
            | objetos_finales:e {:RESULT=e;:}
            ;

objetos_finales ::=  NUM:e {:RESULT=e;:} 
                  | DOUBLE:e {:RESULT=e;:}
                  | RES NUM:e {:RESULT=-e;:} 
                  | RES DOUBLE:e {:RESULT=-e;:}
                  | FALSE {:RESULT=false;:}
                  | TRUE {:RESULT=true;:}
                  ;
tipo ::= PP DOUBLE_TYPE {:RESULT=1;:}
        |PP INT_TYPE {:RESULT=2;:}
        |PP BOOL_TYPE {:RESULT=3;:}
        |PP AC DOUBLE_TYPE CC {:RESULT=1;:}
        |PP AC INT_TYPE CC {:RESULT=2;:}
        |PP AC BOOL_TYPE CC {:RESULT=3;:}
        |
        ;

listaIdent ::= listaIdent COMA IDENT:e {:identificadores.push(e);:}
              | IDENT:e {:RESULT=e;identificadores.push(e);:}
              ;

varLet ::= VAR  {:RESULT=0;:}
         | LET  {:RESULT=1;:}
         ;

array ::= objetos_finales:e COMA array:a {:ArrayList<Object> aux =new ArrayList<Object>();aux.add(e);aux.addAll(a);RESULT=aux;:}
        | objetos_finales:e {:ArrayList<Object> aux =new ArrayList<Object>();aux.add(e);RESULT=aux;:}
        ;





